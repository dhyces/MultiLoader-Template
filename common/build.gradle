plugins {
    id 'idea'
    id "shared.conventions"
    alias(libs.plugins.mdg)
    alias(libs.plugins.moddev)
}

base {
    archivesName = "${mod_name}-common-${libs.versions.minecraft.release.get()}"
}

modsDotGroovy {
    multiplatform {
        gather {
            projectProperty("mod_id")
            projectProperty("mod_name")
            projectProperty("authors")
            projectProperty("at_enabled")
            projectProperty("common_mixin_enabled")
            projectProperty("fabric_mixin_enabled")
            projectProperty("neoforge_mixin_enabled")
        }
        expose()
    }
}

configurations {
    commonJava {
        canBeResolved = false
        canBeConsumed = true
    }
    commonResources {
        canBeResolved = false
        canBeConsumed = true
    }
    testCompileOnly.extendsFrom(compileOnly)
}

neoForge {
    // https://projects.neoforged.net/neoforged/neoform
    neoFormVersion = "1.21.3-20241023.131943"

    if (Boolean.parseBoolean(at_enabled)) {
        accessTransformers.from file("src/main/resources/META-INF/accesstransformer.cfg")
    }
}

dependencies {
    compileOnly libs.mixinextras.common
}

artifacts {
    commonJava sourceSets.main.java.sourceDirectories.singleFile
    sourceSets.main.resources.sourceDirectories.forEach {
        commonResources it
    }
}

sourceSets.main.resources.srcDir 'src/generated/resources'

idea {
    module {
        inheritOutputDirs = false
    }
}